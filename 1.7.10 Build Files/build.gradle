buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}



repositories {
    maven {
        name 'CB Maven FS'
        url "http://chickenbones.net/maven/"
    }
    maven {
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }  
}


apply plugin: 'forge'
apply plugin: 'maven'

version = "v1.2.0Beta22(1.7.10)"
group= "WayofTime.alchemicalWizardry" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Blood Magic"

// Define properties file
ext.configFile = file "build.properties"


configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}
configurations {
    deployerJars
}



dependencies {
    compile "mantle:Mantle:${config.minecraft_version}-${config.mantle_version}:deobf"
    compile "codechicken:CodeChickenLib:${config.minecraft_version}-${config.CCLIB_version}:dev"
    compile "codechicken:CodeChickenCore:${config.minecraft_version}-${config.ccc_version}:dev"
    compile "codechicken:NotEnoughItems:${config.minecraft_version}-${config.NEI_version}:dev"
    compile files("libs/MineTweaker3-Dev-1.7.10-3.0.9.jar")
}


minecraft {
    version = "1.7.10-10.13.0.1152"
    assetDir = "eclipse/assets"
}
processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("maven_url")) {
                logger.info('Publishing to maven repo')
                repository(url: project.maven_url) {
                    authentication(userName: project.maven_username, password: project.maven_password)
                }
            }

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
            }

            pom.project {
                name project.archivesBaseName
                packaging 'jar'
                description 'Blood Magic'
                url 'https://github.com/WayofTime/BloodMagic'

                scm {
                    url 'https://github.com/WayofTime/BloodMagic'
                    connection 'scm:git:git@github.com:WayofTime/BloodMagic.git'
                    developerConnection 'scm:git:git@github.com:WayofTime/BloodMagic.git'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/WayofTime/BloodMagic/issues'
                }

                licenses {
                    license {
                        name 'Creative Commons Attribution 4.0 International'
                        url 'http://creativecommons.org/licenses/by/4.0/legalcode'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'WayofTime'
                        name 'WayofTime'
                        roles {
                            role 'developer'
                        }
                    }
                }
            }
        }
    }
}